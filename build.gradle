plugins {
    // Loom supports 1.21.x; keep in sync with Fabric docs
    id 'fabric-loom' version '1.11.8'
    id 'maven-publish'
}

import org.gradle.api.file.DuplicatesStrategy

version = project.mod_version
group = project.maven_group
base {
    archivesName = project.archives_base_name
}

// ---- Multi-version configuration (1.21.8 â†’ 1.21.10) ----
// Select with: ./gradlew build -PmcVersion=1.21.8 (or 1.21.9, 1.21.10, ...)
def mcVersionSelected = (findProperty('mcVersion') ?: project.findProperty('minecraft_version'))?.toString()
def supported = [
        // Keep these up to date as new Minecraft releases drop
        '1.21.8' : [ yarn: '1.21.8+build.1',  fabricApi: '0.133.0+1.21.8', loader: '0.17.2', modmenu: '15.0.0'],
        '1.21.9' : [ yarn: '1.21.9+build.1',  fabricApi: '0.134.0+1.21.9', loader: '0.17.2', modmenu: '16.0.0-rc.1'],
        '1.21.10': [ yarn: '1.21.10+build.1', fabricApi: '0.135.0+1.21.10', loader: '0.17.3', modmenu: '16.0.0-rc.1'],
]

if (!supported.containsKey(mcVersionSelected)) {
    throw new GradleException("Unsupported mcVersion '${mcVersionSelected}'. Supported: ${supported.keySet()} (set with -PmcVersion)")
}
def vconf = supported[mcVersionSelected]

println "[build] Targeting Minecraft ${mcVersionSelected} (Yarn ${vconf.yarn}, Fabric API ${vconf.fabricApi}, Loader ${vconf.loader})"

loom {
    splitEnvironmentSourceSets()
    mods {
        "ltown" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

// Map MC version to a versioned source tree. Duplicate code per variant is OK.
// Variants use directories like:
//   - src/v1_21/java and src/v1_21/resources
//   - src/v1_21_client/java and src/v1_21_client/resources
def variantFor = { String mc ->
    if (mc.startsWith('1.21.10')) return 'v1_21_10'
    if (mc.startsWith('1.21.9')) return 'v1_21_9'
    if (mc.startsWith('1.21.8')) return 'v1_21_8'
    throw new GradleException("No source variant available for Minecraft ${mc}. Update variantFor in build.gradle.")
}
def variant = variantFor(mcVersionSelected)
def variantMainDir = "src/${variant}"
def variantClientDir = "src/${variant}_client"
println "[build] Using source variant '${variant}' from ${variantMainDir} and ${variantClientDir}"

sourceSets {
    main {
        java.srcDirs = ["${variantMainDir}/java"]
        resources.srcDirs = ["${variantMainDir}/resources"]
    }
    client {
        java.srcDirs = ["${variantClientDir}/java"]
        resources.srcDirs = ["${variantClientDir}/resources"]
    }
}

repositories {
    // Additional maven repos if needed
    maven { url = uri("https://maven.shedaniel.me/") }
    maven { url = uri("https://maven.terraformersmc.com/releases/") }
}

dependencies {
    // Controlled by mcVersion (-PmcVersion=1.21.4 or 1.21.7)
    minecraft "com.mojang:minecraft:${mcVersionSelected}"
    mappings "net.fabricmc:yarn:${vconf.yarn}:v2"
    modImplementation "net.fabricmc:fabric-loader:${vconf.loader}"

    // Fabric API (optional but recommended)
    modImplementation "net.fabricmc.fabric-api:fabric-api:${vconf.fabricApi}"

    // Optional integrations
    if (vconf.modmenu != null) {
        modCompileOnly "com.terraformersmc:modmenu:${vconf.modmenu}"
        modRuntimeOnly "com.terraformersmc:modmenu:${vconf.modmenu}"
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    inputs.property "version", project.version
    inputs.property "minecraft_version", mcVersionSelected
    inputs.property "loader_version", vconf.loader
    filteringCharset = "UTF-8"

    def expandValues = [
        version: project.version,
        minecraft_version: mcVersionSelected,
        loader_version: vconf.loader
    ]

    filesMatching("fabric.mod.json") {
        expand expandValues
    }
}

tasks.named("processClientResources") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    it.options.compilerArgs.add("-Xlint:deprecation")
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
